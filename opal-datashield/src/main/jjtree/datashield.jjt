// Useful sources:
// http://www.ibm.com/developerworks/xml/library/x-javacc2/index.html
// http://www.docstoc.com/docs/22702393/Abstract-Syntax-Trees-Example-Creating-Syntax-Trees-Using-JJTree
// http://www.ibm.com/developerworks/data/library/techarticle/dm-0401brereton/
options{
   IGNORE_CASE=false;
   STATIC=false;
   MULTI=true;
   VISITOR=true;
//   DEBUG_PARSER=true;
}

PARSER_BEGIN(DataShieldGrammar)
package org.obiba.opal.datashield.expr;

import java.io.*;
import java.util.*;

public class DataShieldGrammar {
}

PARSER_END(DataShieldGrammar)

SKIP:
{
   " "
|   "\t"
|   "\r"
|   "\n"
}

TOKEN : /* Numeric Constants */
{
       < S_NUMBER: <FLOAT>
           | <FLOAT> ( ["e","E"] ([ "-","+"])? <FLOAT> )?
       >
 |     < #FLOAT: <INTEGER>
           | <INTEGER> ( "." <INTEGER> )?
           | "." <INTEGER>
       >
 |     < #INTEGER: ( <DIGIT> )+ >
 |     < #DIGIT: ["0" - "9"] >
}

TOKEN:
{
      < S_IDENTIFIER: ( <LETTER> )+ ( <DIGIT> | <LETTER> | <UNDERSCORE>)* >
  |   < S_OPERATOR: (["/", "!"]) | "%*%">
  |   < F_OPERATOR: ([":"] | "%in%") >
  |   < COMMON_OPERATOR: (["+","-","*"]) >
  |   < #LETTER: ["a"-"z", "A"-"Z"] >
  |   < #UNDERSCORE: "_">
}

SimpleNode root() #root :
{}
{
  exp() [";"] <EOF>
  { return jjtThis;}
}

void exp() #void :
{Token op;}
{
 (  LOOKAHEAD(2)
    // Parse a formula as much as possible (A~B+C must not be parsed as (A~B) + C). This is why it comes first here.
    formula()
  | simpleExp() [ (op=<S_OPERATOR> | op=<COMMON_OPERATOR>) exp() {jjtThis.value=op.image;} #BinaryOp(2) ]
 )
}

void simpleExp() #void :
{}
{
 ( LOOKAHEAD(2)
     funcCall()
   | symbol()
   | "(" exp() ")"
 )
}

void funcCall() :
{Token f;}
{
  f = <S_IDENTIFIER> "(" [ expList() ] ")" {jjtThis.value = f.image;}
}

void symbol() : 
{Token s=null;}
{
 (
    s=<S_IDENTIFIER>
  | s=<S_NUMBER>
 ) {jjtThis.value = s.image;}
}

void formula() #symbol :
{Token t;
 StringBuilder ft;
 StringBuilder sb = new StringBuilder();}
{
  (
    t=<S_IDENTIFIER> "~" {sb.append(t.image).append("~");}
    (
        ft=formulaTerm() {sb.append(ft);}
    )
    
  ) {jjtThis.value = sb.toString();}
}

StringBuilder formulaTerm() #void :
{
  Token t;
  StringBuilder sb,ft;
}
{
  (
      "(" sb=formulaTerm() ")"  {sb.insert(0, '(').append(')');}
    | t=<S_IDENTIFIER> {sb = new StringBuilder(t.image);}
  ) [ "^" t=<S_NUMBER> {sb.append("^").append(t.image);}]
    [ (t=<COMMON_OPERATOR> | t=<F_OPERATOR>) ft=formulaTerm() {sb.append(t.image).append(ft);} ]
  {return sb;}
}

void expList() #void :
{}
{
  ( exp() [ "," expList() ] ) 
}
